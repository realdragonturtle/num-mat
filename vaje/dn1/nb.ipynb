{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 41,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using Test\n",
    "\n",
    "import Base.*, Base.\\"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 255,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "4×4 Matrix{Int64}:\n",
       " 1  8  11   0\n",
       " 5  2   9  12\n",
       " 0  6   3  10\n",
       " 0  0   7   4"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "P = PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "copy(P)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    PasovnaMatrika{T}(pasovi, n)\n",
    "\n",
    "Matrika ki ima le nekaj neničelnih pasov\n",
    "Attributes:\n",
    "n - velikost matrike\n",
    "pasovi - Dict{Int, Vector{T}}, kjer so keyi ... -2, -1, 0, 1, 2 ..., oz tisti pasovi ki vsebujejo neničelne vrednosti, \n",
    "            negativne vrednosti označujejo pasove pod diagonalo, 0 je pas diagonale in pozitivni so pasovi nad diagonalo\n",
    "\n",
    "Primer:\n",
    "PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "describes matrix [1 8 11 0; 5 2 9 12; 0 6 3 10; 0 0 7 4]\n",
    "\"\"\"\n",
    "struct PasovnaMatrika{T} <: AbstractArray{T, 2}\n",
    "    pasovi::Dict{Int, Vector{T}} # 0 diagonala, 1, 2, 3...nad diagonalo, -1, -2, ... pod diagonalo\n",
    "    n::Int\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    ZgornjePasovnaMatrika{T}(pasovi, n)\n",
    "\n",
    "Matrika ki ima nekaj neničelnih pasov nad diagonalo\n",
    "Attributes:\n",
    "n - velikost matrike\n",
    "pasovi - Dict{Int, Vector{T}}, kjer so keyi 0, 1, 2 ..., oz tisti pasovi ki vsebujejo neničelne vrednosti, \n",
    "            0 je pas diagonale, pozitivni so pasovi nad diagonalo\n",
    "\n",
    "Primer:\n",
    "PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "describes matrix [1 8 11 0; 0 2 9 12; 0 0 3 10; 0 0 0 4]\n",
    "\"\"\"\n",
    "struct ZgornjePasovnaMatrika{T} <: AbstractArray{T, 2}\n",
    "    pasovi::Dict{Int, Vector{T}} \n",
    "    n::Int\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    SpodnjePasovnaMatrika{T}(pasovi, n)\n",
    "\n",
    "Matrika ki ima nekaj neničelnih pasov pod diagonalo\n",
    "Attributes:\n",
    "n - velikost matrike\n",
    "pasovi - Dict{Int, Vector{T}}, kjer so keyi ... -2, -1, 0, oz tisti pasovi ki vsebujejo neničelne vrednosti, \n",
    "            negativne vrednosti označujejo pasove pod diagonalo, 0 je pas diagonale \n",
    "\n",
    "Primer:\n",
    "PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7])]), 4)\n",
    "describes matrix [1 0 0 0; 5 2 0 0; 0 6 3 0; 0 0 7 4]\n",
    "\"\"\"\n",
    "struct SpodnjePasovnaMatrika{T} <: AbstractArray{T, 2}\n",
    "    pasovi::Dict{Int, Vector{T}} \n",
    "    n::Int\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    size(M)\n",
    "\n",
    "Vrne velikost pasovne matrike M\n",
    "\"\"\"\n",
    "function Base.size(M::PasovnaMatrika)\n",
    "    return (M.n, M.n)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    size(M)\n",
    "\n",
    "Vrne velikost zgornje pasovne matrike M\n",
    "\"\"\"\n",
    "function Base.size(M::ZgornjePasovnaMatrika)\n",
    "    return (M.n, M.n)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    size(M)\n",
    "\n",
    "Vrne velikost spodnje pasovne matrike M\n",
    "\"\"\"\n",
    "function Base.size(M::SpodnjePasovnaMatrika)\n",
    "    return (M.n, M.n)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    getindex(M, I(2))\n",
    "\n",
    "Vrne element pasovne matrike na mestu I\n",
    "\"\"\"\n",
    "function Base.getindex(M::PasovnaMatrika, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1]\n",
    "    if haskey(M.pasovi, pas)\n",
    "        if pas >= 0\n",
    "            return M.pasovi[pas][I[1]]\n",
    "        else \n",
    "            return M.pasovi[pas][I[2]]\n",
    "        end\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    getindex(M, I(2))\n",
    "\n",
    "Vrne element zgornje pasovne matrike na mestu I\n",
    "\"\"\"\n",
    "function Base.getindex(M::ZgornjePasovnaMatrika, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1]\n",
    "    if haskey(M.pasovi, pas)\n",
    "        return M.pasovi[pas][I[1]]\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    getindex(M, I(2))\n",
    "\n",
    "Vrne element spodnje pasovne matrike na mestu I\n",
    "\"\"\"\n",
    "function Base.getindex(M::SpodnjePasovnaMatrika, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1] \n",
    "    if haskey(M.pasovi, pas)\n",
    "        return M.pasovi[pas][I[2]]\n",
    "    else\n",
    "        return 0\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 22,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    setindex(M, v, I(2))\n",
    "\n",
    "Nastavi element na mestu I na vrednost v\n",
    "Vrne napako če spreminjamo vrednosti v ničelih pasovih\n",
    "\"\"\"\n",
    "function Base.setindex!(M::PasovnaMatrika, v, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1]\n",
    "    if haskey(M.pasovi, pas)\n",
    "        if pas >= 0\n",
    "            M.pasovi[pas][I[1]] = v\n",
    "        else \n",
    "            M.pasovi[pas][I[2]] = v\n",
    "        end\n",
    "    else\n",
    "        return error(\"Vrednosti se lahko nastavi samo v neničelnih pasovih\") # dodaj cel pas naenkrat? -> setindex!(A, X, I...)\n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    setindex(M, v, I(2))\n",
    "\n",
    "Nastavi element na mestu I na vrednost v\n",
    "Vrne napako če spreminjamo vrednosti v ničelih pasovih\n",
    "\"\"\"\n",
    "function Base.setindex!(M::SpodnjePasovnaMatrika, v, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1]\n",
    "    if haskey(M.pasovi, pas)\n",
    "        M.pasovi[pas][I[2]] = v\n",
    "    else\n",
    "        return error(\"Vrednosti se lahko nastavi samo v neničelnih pasovih\") \n",
    "    end\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    setindex(M, v, I(2))\n",
    "\n",
    "Nastavi element na mestu I na vrednost v\n",
    "Vrne napako če spreminjamo vrednosti v ničelih pasovih\n",
    "\"\"\"\n",
    "function Base.setindex!(M::ZgornjePasovnaMatrika, v, I::Vararg{Int, 2})\n",
    "    pas = I[2] - I[1]\n",
    "    if haskey(M.pasovi, pas)\n",
    "        M.pasovi[pas][I[1]] = v\n",
    "    else\n",
    "        return error(\"Vrednosti se lahko nastavi samo v neničelnih pasovih\") \n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 57,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "* (generic function with 367 methods)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    M*v\n",
    "\n",
    "Zmnoži pasovno matriko M z vektorjem v\n",
    "\"\"\"\n",
    "function *(M::PasovnaMatrika, v::Vector)\n",
    "    y = zeros(length(v))\n",
    "    for (k, pas) in M.pasovi\n",
    "        for i=1:length(pas)\n",
    "            row = k<=0 ? -k+i : i # indeks elemneta v pasu\n",
    "            col = k<=0 ? i : k+i # indeks elemnta v pasu\n",
    "            y[row] += v[col] * pas[i]\n",
    "        end\n",
    "    end\n",
    "    return y\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    M*v\n",
    "\n",
    "Zmnoži tgornje pasovno matriko M z vektorjem v\n",
    "\"\"\"\n",
    "function *(M::ZgornjePasovnaMatrika, v::Vector)\n",
    "    y = zeros(length(v))\n",
    "    for (k, pas) in M.pasovi\n",
    "        for i=1:length(pas)\n",
    "            row = i # indeks elemneta v pasu\n",
    "            col = k+i # indeks elemnta v pasu\n",
    "            y[row] += v[col] * pas[i]\n",
    "        end\n",
    "    end\n",
    "    return y\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    M*v\n",
    "\n",
    "Zmnoži spodnje pasovno matriko M z vektorjem v\n",
    "\"\"\"\n",
    "function *(M::SpodnjePasovnaMatrika, v::Vector)\n",
    "    y = zeros(length(v))\n",
    "    for (k, pas) in M.pasovi\n",
    "        for i=1:length(pas)\n",
    "            row = -k+i # indeks elemneta v pasu\n",
    "            col = i # indeks elemnta v pasu\n",
    "            y[row] += v[col] * pas[i]\n",
    "        end\n",
    "    end\n",
    "    return y\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 197,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "\\ (generic function with 138 methods)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "\"\"\"\n",
    "    M\\\\b\n",
    "\n",
    "Izračuna rešitev sistema Mx=b za pasovno matriko M\n",
    "\"\"\"\n",
    "function \\(M_in::PasovnaMatrika, b::Vector)\n",
    "    bc = deepcopy(b)\n",
    "    M = deepcopy(M_in)\n",
    "    z_pas = maximum(M.pasovi.keys)\n",
    "    l_pas = minimum(M.pasovi.keys)\n",
    "\n",
    "    for j=1:M.n\n",
    "        for i=j+1:min(M.n, -l_pas+j)\n",
    "            l = M[i, j] / M[j, j]\n",
    "            M[i, j] = 0\n",
    "            for k=j+1:min(z_pas+j+1, M.n)\n",
    "                M[i, k] -= l * M[j, k]\n",
    "            end\n",
    "            bc[i] -= bc[j] * l \n",
    "        end\n",
    "    end\n",
    "\n",
    "    x = zeros(M.n)\n",
    "    for i=M.n:-1:1\n",
    "        curr = bc[i] \n",
    "        for ii=M.n:-1:i\n",
    "            curr -= M[i, ii] * x[ii]\n",
    "        end\n",
    "        x[i] = curr / M[i, i]\n",
    "    end\n",
    "    return x\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    M\\\\b\n",
    "\n",
    "Izračuna rešitev sistema Mx=b za spodnje pasovno matriko M\n",
    "\"\"\"\n",
    "function \\(M::SpodnjePasovnaMatrika, b::Vector)\n",
    "\n",
    "    x = zeros(M.n)\n",
    "    for i=1:M.n\n",
    "        curr = b[i] \n",
    "        for ii=1:(i-1)\n",
    "            curr -= M[i, ii] * x[ii]\n",
    "        end\n",
    "        x[i] = curr / M[i, i]\n",
    "    end\n",
    "    return x\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    M\\\\b\n",
    "\n",
    "Izračuna rešitev sistema Mx=b za zgornje pasovno matriko M\n",
    "\"\"\"\n",
    "function \\(M::ZgornjePasovnaMatrika, b::Vector)\n",
    "\n",
    "    x = zeros(M.n)\n",
    "    for i=M.n:-1:1\n",
    "        curr = b[i] \n",
    "        for ii=M.n:-1:i\n",
    "            curr -= M[i, ii] * x[ii]\n",
    "        end\n",
    "        x[i] = curr/ M[i, i]\n",
    "    end\n",
    "    return x\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 242,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "lu (generic function with 12 methods)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "import LinearAlgebra.lu\n",
    "\n",
    "\"\"\"\n",
    "    lu(M)\n",
    "\n",
    "Izračuna LU razcep pasovne matrike M, vrne zgornje pasovno matriko U in spodnje pasovno matriko L\n",
    "L, U = lu(M)\n",
    "\"\"\"\n",
    "function lu(M::PasovnaMatrika)\n",
    "    p = Dict{Int64, Vector{Float64}}()\n",
    "    for (k, pas) in M.pasovi\n",
    "        if k<0\n",
    "            p[k] = zeros(length(pas))\n",
    "        elseif  k==0\n",
    "            p[k] = ones(length(pas))\n",
    "        end\n",
    "    end\n",
    "    L = SpodnjePasovnaMatrika(p, M.n)\n",
    "    \n",
    "    l_pas = minimum(M.pasovi.keys)\n",
    "    z_pas = maximum(M.pasovi.keys)\n",
    "    A = deepcopy(M)\n",
    "    \n",
    "    for j=1:M.n\n",
    "        for i=j+1:-l_pas+1\n",
    "            l = A[i, j] / A[j,j]\n",
    "            L[i, j] = l\n",
    "\n",
    "            for k=j+1:min(z_pas+j+1, M.n)\n",
    "                if A[j,j] < A[j,k]\n",
    "                    error(\"Ni diagonalno dominantna\")\n",
    "                end\n",
    "                A[i, k] = A[i, k] - l * A[j, k]\n",
    "            end\n",
    "        end\n",
    "    end \n",
    "\n",
    "    pu = Dict{Int64, Vector{Float64}}()\n",
    "    for (k, pas) in A.pasovi\n",
    "        if  k==0\n",
    "            pu[k] = copy(pas)\n",
    "        elseif k>0\n",
    "            pu[k] = copy(pas)\n",
    "        end\n",
    "    end\n",
    "    U = ZgornjePasovnaMatrika(pu, M.n)\n",
    "    return (L, U)\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 253,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[0m\u001b[1mTest Summary: | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "pasovna       | \u001b[32m  26  \u001b[39m\u001b[36m   26\u001b[39m\n",
      "\u001b[0m\u001b[1mTest Summary:   | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "zgornje-pasovna | \u001b[32m  23  \u001b[39m\u001b[36m   23\u001b[39m\n",
      "\u001b[0m\u001b[1mTest Summary:   | \u001b[22m\u001b[32m\u001b[1mPass  \u001b[22m\u001b[39m\u001b[36m\u001b[1mTotal\u001b[22m\u001b[39m\n",
      "spodnje-pasovna | \u001b[32m  23  \u001b[39m\u001b[36m   23\u001b[39m\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "Test.DefaultTestSet(\"spodnje-pasovna\", Any[], 23, false, false)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "@testset \"pasovna\" begin\n",
    "    eps = 1e-6\n",
    "\n",
    "    M = [1 8 11 0; 5 2 9 12; 0 6 3 10; 0 0 7 4]\n",
    "    P = PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "    for i=1:4, j=1:4\n",
    "        @test P[i, j] == M[i, j] # test get index?\n",
    "    end\n",
    "    @test size(P) == size(M) # test size\n",
    "\n",
    "    for a = [1, 2], b = [1, 3]\n",
    "        val = a+b\n",
    "        P[a, b] = val\n",
    "        @test P[a, b] == val\n",
    "    end\n",
    "\n",
    "    # *\n",
    "    P = PasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "    v = [1, 2, 3, 4]\n",
    "    @test norm(P*v - M*v) < eps\n",
    "\n",
    "    P = PasovnaMatrika(Dict([(0, [1.0, 2, 3, 4]), (-1, [5.0, 6, 7]), (1, [8.0, 9, 10]), (2, [11.0, 12])]), 4) # float\n",
    "    v = [1, 2, 3, 4.0]\n",
    "    x = P\\v\n",
    "    @test norm(P*x - v) < eps\n",
    "\n",
    "    # lu\n",
    "    P = PasovnaMatrika(Dict([(0,[10.0, 15.0, 9.0]), (-1,[4.0, 8.0]), (1, [2.0, 6.0]), (-2, [7.0])]), 3)\n",
    "    M = [10 2 0; 4 15 6; 7 8 9]\n",
    "    L, U = lu(P)\n",
    "    @test norm(L*U - P) < eps\n",
    "    Lm, Um = lu(M)\n",
    "    @test norm(L - Lm) < eps\n",
    "    @test norm(U - Um) < eps\n",
    "\n",
    "end\n",
    "\n",
    "@testset \"zgornje-pasovna\" begin\n",
    "    eps = 1e-6\n",
    "\n",
    "    M = [1 8 11 0; 0 2 9 12; 0 0 3 10; 0 0 0 4]\n",
    "    P = ZgornjePasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "    for i=1:4, j=1:4\n",
    "        @test P[i, j] == M[i, j] # test get index?\n",
    "    end\n",
    "    @test size(P) == size(M) # test size\n",
    "\n",
    "    for a = [1, 2], b = [2, 3]\n",
    "        val = a+b\n",
    "        P[a, b] = val\n",
    "        @test P[a, b] == val\n",
    "    end\n",
    "\n",
    "    P = ZgornjePasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (1, [8, 9, 10]), (2, [11, 12])]), 4)\n",
    "    v = [1, 2, 3, 4]\n",
    "    @test norm(P*v - M*v) < eps\n",
    "\n",
    "    P = ZgornjePasovnaMatrika(Dict([(0, [1.0, 2, 3, 4]), (1, [8.0, 9, 10]), (2, [11.0, 12])]), 4)\n",
    "    v = [1, 2, 3, 4.0]\n",
    "    x = P\\v\n",
    "    @test norm(P*x - v) < eps\n",
    "end\n",
    "\n",
    "@testset \"spodnje-pasovna\" begin\n",
    "    eps = 1e-6\n",
    "\n",
    "    M = [1 0 0 0; 5 2 0 0; 11 6 3 0; 0 12 7 4]\n",
    "    P = SpodnjePasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (-2, [11, 12])]), 4)\n",
    "    for i=1:4, j=1:4\n",
    "        @test P[i, j] == M[i, j] # test get index?\n",
    "    end\n",
    "    @test size(P) == size(M) # test size\n",
    "\n",
    "    for a = [3, 4], b = [2, 3]\n",
    "        val = a+b\n",
    "        P[a, b] = val\n",
    "        @test P[a, b] == val\n",
    "    end\n",
    "\n",
    "    P = SpodnjePasovnaMatrika(Dict([(0, [1, 2, 3, 4]), (-1, [5, 6, 7]), (-2, [11, 12])]), 4)\n",
    "    v = [1, 2, 3, 4]\n",
    "    @test norm(P*v - M*v) < eps\n",
    "\n",
    "    P = SpodnjePasovnaMatrika(Dict([(0, [1.0, 2, 3, 4]), (-1, [5.0, 6, 7]), (-2, [11.0, 12])]), 4)\n",
    "    v = [1, 2, 3, 4.0]\n",
    "    x = P\\v\n",
    "    @test norm(P*x - v) < eps\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
